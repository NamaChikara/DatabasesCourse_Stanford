In this assignment you are to write relational algebra queries over a small database.
The database contains four relations:

    Person(name, age, gender)       // name is a key
    Frequents(name, pizzeria)       // [name,pizzeria] is a key
    Eats(name, pizza)               // [name,pizza] is a key
    Serves(pizzeria, pizza, price)  // [pizzeria,pizza] is a key

--------------------------------------------------------------------------------------

Q1: Find all pizzas eaten by at least one female over the age of 20. 

\project_{pizza}(\select_{gender = 'female' and age > 20}(Person \join Eats))

--------------------------------------------------------------------------------------

Q2: Find the names of all females who eat at least one pizza served by Straw Hat. 

\project_{name}(
		\select_{gender='female'}(
    			(\select_{pizzeria='Straw Hat'}Serves \join Eats) 
				\join 
			Person
		)
)

--------------------------------------------------------------------------------------

Q3: Find all pizzerias that serve at least one pizza for less than $10 that either Amy 
    or Fay (or both) eat. 

\project_{pizzeria}(
    \select_{name='Amy' or name='Fay'}(
        (\select_{price<10}Serves) \join Eats
    )
)

--------------------------------------------------------------------------------------

Q4: Find all pizzerias that serve at least one pizza for less than $10 that both Amy 
    and Fay eat. 

\project_{pizzeria}(
    \select_{n1='Amy' and n2='Fay'}(
        (
            \rename_{n1, pizzeria}(\project_{name, pizzeria}(\select_{price<10}Serves \join Eats))
        )
        \join
        (
            \rename_{n2, pizzeria}(\project_{name, pizzeria}(\select_{price<10}Serves \join Eats))
        )
    )
)

--------------------------------------------------------------------------------------

Q5: Find the names of all people who eat at least one pizza served by Dominos but who 
    do not frequent Dominos. 

\project_{name}((\select_{pizzeria='Dominos'}Serves) \join Eats)
    \diff
\project_{name}(\select_{pizzeria='Dominos'}Frequents)

--------------------------------------------------------------------------------------

Q6: Find all pizzas that are eaten only by people younger than 24, or that cost less 
    than $10 everywhere they're served. 

(
 \project_{pizza}(\select_{age<24}(Person \join Eats)) 
    \diff
 \project_{pizza}(\select_{age>23}(Person \join Eats))
)
    \union
(
 \project_{pizza}(\select_{price<10}Serves)
    \diff
 \project_{pizza}(\select_{price>=10}Serves)
)


--------------------------------------------------------------------------------------

Q7: Find the age of the oldest person (or people) who eat mushroom pizza. 

\rename_{age2}(\project_{age}(\select_{pizza='mushroom'}(Person \join Eats)))
    \diff
\project_{age2}
    (
    \rename_{age1}(\project_{age}(\select_{pizza='mushroom'}(Person \join Eats)))
        \join_{age2<age1}
    \rename_{age2}(\project_{age}(\select_{pizza='mushroom'}(Person \join Eats)))
    )

--------------------------------------------------------------------------------------

Q8: Find all pizzerias that serve only pizzas eaten by people over 30. 

\project_{pizzeria}Serves 
    \diff
\project_{pizzeria}
(
    (
        \project_{pizza}Serves
            \diff
        \project_{pizza}(\select_{age>30}(Eats \join Person))
    )
    \join
    Serves
)
